plugins {
    id "java"
    id "org.springframework.boot" version "3.2.5"
    id "org.jetbrains.kotlin.jvm" version "1.9.23"
    id "org.liquibase.gradle" version "2.2.2"
    id "org.jetbrains.kotlin.plugin.serialization" version "2.0.20-RC2"
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        url = "https://jitpack.io"
    }

    maven {
        url = "https://repo.jenkins-ci.org/releases/"
    }

    maven {
        name "m2-dv8tion"
        url "https://m2.dv8tion.net/releases"
    }

    maven {
        name "arbjergDev-snapshots"
        url "https://maven.lavalink.dev/snapshots"
    }

    maven {
        name "arbjergDev"
        url "https://maven.lavalink.dev/releases"
    }
}

compileJava.options.encoding = "UTF-8"

dependencies {
    implementation "ch.qos.logback:logback-classic:1.5.6"

    implementation "com.google.apis:google-api-services-youtube:v3-rev20240417-2.0.0"
    implementation 'com.google.guava:guava:33.1.0-jre'
    implementation "com.sedmelluq:jda-nas:1.1.0"
    implementation "dev.arbjerg:lavaplayer:2.2.1"
    implementation "dev.arbjerg:lavaplayer-ext-youtube-rotator:2.2.1"
    implementation "dev.lavalink.youtube:common:1.5.2"

    implementation "commons-validator:commons-validator:1.8.0"

    implementation 'io.github.resilience4j:resilience4j-ratelimiter:2.2.0'

    implementation "io.ktor:ktor-client-cio:2.3.12"
    implementation "io.ktor:ktor-client-content-negotiation:2.3.12"
    implementation "io.ktor:ktor-client-core:2.3.12"
    implementation "io.ktor:ktor-serialization-kotlinx-json:2.3.12"

    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0-RC.2"

    implementation "net.dv8tion:JDA:5.0.0-beta.23"
    implementation "com.github.robinfriedli:exec:1.4"
    implementation "com.github.robinfriedli:JXP:2.0.5"
    implementation "com.github.robinfriedli:StringList:1.6.1"
    implementation "com.github.robinfriedli:ThreadPool:1.2.0"

    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-text:1.12.0"
    implementation "org.codehaus.groovy:groovy-all:3.0.21"
    implementation "com.github.robinfriedli:topgg-java-sdk:1a1af5964a"
    implementation "org.ehcache:ehcache:3.10.8"
    implementation "org.hibernate:hibernate-core:6.5.0.Final"
    implementation "org.hibernate:hibernate-jcache:6.5.0.Final"
    implementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
    implementation "org.liquibase:liquibase-core:4.27.0"
    implementation "org.postgresql:postgresql:42.7.3"
    implementation "org.quartz-scheduler:quartz:2.3.2"
    implementation 'org.springframework.boot:spring-boot-dependencies:3.2.5'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.2.5'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:3.2.5'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.2.5'
    implementation 'org.springframework:spring-core:6.1.6'
    implementation 'org.json:json:20240303'

    // check if IPlaylistItem implementations were added when upgrading and update SpotifyTrack accordingly
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:9.0.0-RC1'

    liquibaseRuntime "org.liquibase:liquibase-core:4.27.0"
    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate6:4.27.0"
    liquibaseRuntime sourceSets.main.runtimeClasspath
    liquibaseRuntime sourceSets.main.output

    testImplementation 'org.testng:testng:7.10.2'
    testImplementation "com.google.truth:truth:1.4.2"

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:3.2.5'
}

test {
    useTestNG()
}

group = "aiode"
version = "1.0-SNAPSHOT"
description = "aiode"
sourceCompatibility = "21"
targetCompatibility = "21"

jar {
    manifest {
        attributes(
            "Main-Class": "net.robinfriedli.aiode.boot.SpringBootstrap"
        )
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "21"
    }
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/webapp"
        }
    }
}

def applicationProperties = new Properties()
file("src/main/resources/application.properties").withInputStream { applicationProperties.load(it) }
def privateProperties = new Properties()
file("src/main/resources/settings-private.properties").withInputStream { privateProperties.load(it) }

bootRun {
    jvmArgs("-Xmx" + applicationProperties.getProperty("aiode.preferences.max_heap_size"))
}

liquibase {
    activities {
        main {
            changeLogFile applicationProperties.getProperty("liquibase.change-log-path")
            referenceUrl applicationProperties.getProperty("liquibase.referenceUrl")
            url applicationProperties.getProperty("spring.datasource.url")
            username privateProperties.getProperty("spring.datasource.username")
            password privateProperties.getProperty("spring.datasource.password")
            referenceDriver "liquibase.ext.hibernate.database.connection.HibernateDriver"
        }
    }
}
